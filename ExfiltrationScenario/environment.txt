% ****************** Environment Description ******************

% ********* Context Description *********

% ***** Types and Instances *****

% m1 and m2 are computers
comp(m1;m2).

%employees
emp(alison;jean;bob).

% urls
url(url1;url2).

% browsers
browser(b1;b2).

% employees' emails
addr(aalison;ajean).

% sender and recipient emails
sendAddr(aalison;ajean).
recAddr(aalison;ajean).

% external email addresses
extAddr(abob;atuck).


% external sender and recipient email addresses
sendExtAddr(abob;atuck).
recExtAddr(abob;atuck).


% email body
email(e1).

% employees' roles
role(president;cfo).

% passwords for employees electronic identities
pwd(pwdJean;pwdAlison).

% confidential file
fi(m57plan).

% external storage device
st(usb1).

% applications
app(app1;app2).


% ***** Context Relations *****

% An employee is logged on a computer

fluent(logged(E,C)):-
    emp(E), comp(C).

%A employee has the permission to access to a computer

fluent(hasPermission(E,C)):-
    emp(E), comp(C).


%A file is stored on a computer

fluent(isStoredIn(F,C)):-
    fi(F), comp(C).

%An employee has an email address

fluent(hasEmail(P,E)):-
    emp(P), addr(E).

fluent(hasEmail(P,E)):-
    emp(P), sendAddr(E).

fluent(hasEmail(P,E)):-
    emp(P), recAddr(E).

%An employee has a password associated with his/her email address
fluent(hasPwd(E,P)):-
    emp(E), pwd(P).

%An employee has a role
fluent(hasRole(E,R)):-
    emp(E), role(R).

%A storage device is mounted on a computer
fluent(mounted(S,C)):-
    st(S), comp(C).


% An application is a malware
fluent(isMalware(A)):-
    app(A).

% A url is blacklisted
fluent(isBlacklisted(U)):-
    url(U).

% An application is installed on a computer
fluent(installed(A,C)):-
    app(A), comp(C).

% A browser is installed on a computer
fluent(isInstalled(B,C)):-
    browser(B), comp(C).


% ********* Behaviour Description *********

% ***** Primitive Events *****

event(V):-
    pe(V).

% From a computer hard drive it is possible to infer that an email E with an 
% attachment A is sent from an internal corporate address S to an internal 
% corporate address R
pe(send_email_e2e(E,A,S,R,C)):-
    email(E), fi(A), sendAddr(S), recAddr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E with an 
% attachment A is sent from an internal corporate address S to an external 
% address R
pe(send_email_e2ext(E,A,S,R,C)):-
    email(E), fi(A), addr(S), extAddr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E with an 
% attachment A is sent from an external address S to an internal corporate 
% address R
pe(send_email_ext2e(E,A,S,R,C)):-
    email(E), fi(A), extAddr(S), addr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E with an 
% attachment A is sent from an external address S to an external address R
pe(send_email_ext2ext(E,A,S,R,C)):-
    email(E), fi(A), sendExtAddr(S), recExtAddr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E is received 
% by an internal corporate address S from an internal corporate address R
pe(rec_email_req_e2e(E,S,R,C)):-
    email(E), sendAddr(S), recAddr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E is received 
% by an internal corporate address S from an external address R
pe(rec_email_req_e2ext(E,S,R,C)):-
    email(E), addr(S), extAddr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E is received 
% by an external address S from an internal corporate address R
pe(rec_email_req_ext2e(E,S,R,C)):-
    email(E), extAddr(S), addr(R), comp(C).

% From a computer hard drive it is possible to infer that an email E is received 
% by an external address S from an external address R
pe(rec_email_req_ext2ext(E,S,R,C)):-
    email(E), sendExtAddr(S), recExtAddr(R), comp(C).


% From the system log of computer C it is possible to infer that an employee E 
% performed the login using her password P
pe(sys_login(E,P, C)):-
    emp(E), pwd(P), comp(C).

% From the system log of computer C it is possible to infer that an employee E 
% did the logout
pe(sys_logout(E, C)):-
    emp(E), comp(C).


% From the system log of computer C it is possible to infer that a storage device S
% was mounted
pe(sys_mount(S,C)):-
    st(S), comp(C).

% From the system log of computer C it is possible to infer that a storage device S
% was unmounted
pe(sys_unmount(S,C)):-
    st(S), comp(C).

% From the system log of computer C it is possible to infer that employee E copied 
% file F
pe(sys_copy(E, F, C)):-
    emp(E), fi(F), comp(C).

% From the hard drive of computer C it is possible to infer that application A was installed
pe(sys_install(A,C)):-
    app(A), comp(C).

% From the hard drive of computer C it is possible to infer that application A was uninstalled
pe(sys_uninstall(A,C)):-
    app(A), comp(C).

% From the web history of computer C it is possible to infer that application a connection to a blacklisted
% url from browser B was performed
pe(web_connection(B,U,C)):-
    browser(B), url(U), comp(C).


% ***** Complex Events *****

event(V):-
    ce(V).

% An email with attachment A is sent by an employee P1 to employee P2
ce(emailSentE2E(E,A,P1,P2)):-
    email(E), fi(A), emp(P1), emp(P2).

% An email with attachment A is sent by an employee P to an external address R
ce(emailSentE2Ext(E,A,P,R)):-
    email(E), fi(A), emp(P), extAddr(R).

% An email with attachment A is sent by an employee P to an external address R
% (Sender and recipient addresses are the same)
ce(emailSentExt2ExtS(E,A,P,R)):-
    email(E), fi(A), emp(P), extAddr(R).

% An email with attachment A is sent by an employee P to an external address R
% (Sender and recipient addresses are the different)
ce(emailSentExt2ExtD(E,A,P,R)):-
    email(E), fi(A), emp(P), extAddr(R).

% An email is received by an employee P1 from another employee P2
ce(recEmailReqE2E(E,P1,P2)):-
    email(E), emp(P1), emp(P2).

% An email is received by an employee P from an external address S
ce(recEmailReqExt2E(E,S,P)):-
    email(E), extAddr(S), emp(P).


% An email previously received by employee P1 from P2 is answered by also including 
% an attachment A
ce(answerEmailE2E(E1,E2,A,P1,P2)):-
    email(E1), email(E2), fi(A), emp(P1), emp(P2).

% An email previously received by employee P from an external address S is answered by also including 
% an attachment A
ce(answerEmailE2Ext(E1,E2,A,P,S)):-
    email(E1), email(E2), fi(A), emp(P), extAddr(S).


% An employee logins to a computer
ce(login(E,C)):-
    emp(E), comp(C).

% An employee logouts from a computer
ce(logout(E,C)):-
    emp(E), comp(C).

% A device is mounted on a computer
ce(mount(S,C)):-
    st(S), comp(C).

% A device is unmounted from a computer
ce(unmount(S,C)):-
    st(S), comp(C).

% An employee copies a file on a computer
ce(copy(E,F,C)):-
    emp(E), fi(F), comp(C).

% An application is installed on a computer
ce(install(A,C)):-
    app(A),comp(C).

% An application is uninstalled from a computer
ce(uninstall(A,C)):-
    app(A),comp(C).

% A web connection to url U is performed through a browser B installed on a computer 
ce(webConnection(B,U,C)):-
    browser(B), url(U), comp(C).



% ***** Composite Definitions *****

% An employee who is entitled to access to computer C
% performs the login

happens(login(E,C),T,TR):-
    emp(E),
    comp(C),
    time(T),
    trace(TR),
    emp(E2),
    happens(sys_login(E,P,C),T,TR),
    holdsAt(hasPermission(E,C),T,TR),
    holdsAt(hasPwd(E,P),T,TR).


% An employee who is entitled to access to computer C
% and is currently logged to C performs the logout

happens(logout(E,C),T,TR):-
    emp(E),
    comp(C),
    time(T),
    trace(TR),
    happens(sys_logout(E,C),T,TR),
    holdsAt(logged(E,C),T,TR).


% An email with attachment A is sent by an employee P1, who is currently logged on computer C,
% to an employee P2 by using an internal corporate address

happens(emailSentE2E(E,A,P1,P2),T,TR):-
    trace(TR),
    email(E),
    fi(A),
    sendAddr(S),
    recAddr(R),
    time(T),
    comp(C),
    emp(P1), emp(P2),
    happens(send_email_e2e(E,A,S,R,C),T,TR),
    holdsAt(hasEmail(P1,S),T,TR),
    holdsAt(logged(P1,C),T,TR),
    holdsAt(hasEmail(P2,R),T,TR).

% An email with attachment A is sent by an employee P1, who is currently logged on computer C,
% to an employee P2 by using an external address

happens(emailSentE2E(E,A,P1,P2),T,TR):-
    trace(TR),
    email(E),
    fi(A),
    extAddr(S),
    addr(R),
    time(T),
    comp(C),
    emp(P1), emp(P2),
    happens(send_email_ext2e(E,A,S,R,C),T,TR),
    holdsAt(hasEmail(P2,R),T,TR),
    holdsAt(logged(P1,C),T,TR).


% An email with attachment A is sent to an external email address R by an employee
% P who is  using his/her internal corporate address and is currently logged on computer C

happens(emailSentE2Ext(E,A,P,R),T,TR):-
    trace(TR),
    email(E),
    fi(A),
    addr(S),
    extAddr(R),
    time(T),
    comp(C),
    emp(P),
    happens(send_email_e2ext(E,A,S,R,C),T,TR),
    holdsAt(hasEmail(P,S),T,TR),
    holdsAt(logged(P,C),T,TR).

% An email with attachment A is sent to an external email address by an employee
% who is  using an external email account and is currently logged on computer C
% (Sender and recipient addresses are the same)

happens(emailSentExt2ExtS(E,A,P,R),T,TR):-
    trace(TR),
    email(E),
    fi(A),
    sendExtAddr(S),
    recExtAddr(R),
    S == R,
    time(T),
    comp(C),
    emp(P),
    happens(send_email_ext2ext(E,A,S,R,C),T,TR),
    holdsAt(logged(P,C),T,TR).


% An email with attachment A is sent to an external email address by an employee
% who is  using an external email account and is currently logged on computer C
% (Sender and recipient addresses are different)

happens(emailSentExt2ExtD(E,A,P,R),T,TR):-
    email(E),
    fi(A),
    sendExtAddr(S),
    recExtAddr(R),
    S != R,
    time(T),
    comp(C),
    emp(P),
    trace(TR),
    happens(send_email_ext2ext(E,A,S,R,C),T,TR),
    holdsAt(logged(P,C),T,TR).


%An email is received by an employee P1 from another employee P2
happens(recEmailReqE2E(E,P1,P2),T,TR):-
    trace(TR),
    email(E),
    emp(P1),
    emp(P2),
    comp(C),
    time(T),
    sendAddr(S),
    recAddr(R),
    happens(rec_email_req_e2e(E,S,R,C),T,TR),
    holdsAt(hasEmail(P2,R),T,TR),
    holdsAt(hasEmail(P1,S),T,TR),
    holdsAt(logged(P2,C),T,TR).

% An email is received by an employee, who is currently logged to a computer, from another employee.
% The email was sent to an external email account 

happens(recEmailReqE2E(E,P1,P2),T,TR):-
    trace(TR),
    email(E),
    emp(P1),
    emp(P2),
    comp(C),
    time(T),
    addr(S),
    extAddr(R),
    happens(rec_email_req_e2ext(E,S,R,C),T,TR),
    holdsAt(hasEmail(P1,S),T,TR),
    holdsAt(logged(P2,C),T,TR).

%An email is received by an employee, who is currently logged to a computer, from an external email address

happens(recEmailReqExt2E(E,S,P),T,TR):-
    trace(TR),
    email(E),
    emp(P),
    time(T),
    comp(C),
    extAddr(S),
    addr(R),
    happens(rec_email_req_ext2e(E,S,R,C),T,TR),
    holdsAt(hasEmail(P,R),T,TR),
    holdsAt(logged(P,C),T,TR).

% An email is received by an employee P, who is currently logged to a computer, from an external email address
% to an external email account
% (Email address of the sender and recipient are different) 

happens(recEmailReqExt2E(E,S,P),T,TR):-
    trace(TR),
    email(E),
    emp(P),
    time(T),
    comp(C),
    sendExtAddr(S),
    recExtAddr(R),
    S != R,
    happens(rec_email_req_ext2ext(E,S,R,C),T,TR),
    holdsAt(logged(P,C),T,TR).



% An email previously received by employee P1 from P2 is answered by also including 
% an attachment A

happens(answerEmailE2E(E1,E2,A,P1,P2),T2,TR):-
    trace(TR),
    email(E1),
    email(E2),
    P1 != P2,
    fi(A),
    emp(P1),
    emp(P2),
    time(T1),
    time(T2),
    T2 > T1,
    happens(recEmailReqE2E(E1,P2,P1),T1,TR),
    happens(emailSentE2E(E2,A,P1,P2),T2,TR).


% An email previously received by employee P from an external address S is answered by also including 
% an attachment A and using an internal corporate address

happens(answerEmailE2Ext(E1,E2,A,P,S),T2,TR):-
    trace(TR),
    email(E1),
    email(E2),
    fi(A),
    emp(P),
    extAddr(S),
    time(T2),
    time(T1),
    T2 > T1,
    happens(emailSentE2Ext(E2,A,P1,S),T2,TR),
    happens(recEmailReqExt2E(E1,S,P),T1,TR).

% An email previously received by employee P from an external address S is answered by also including 
% an attachment A using an external address

happens(answerEmailE2Ext(E1,E2,A,P,S),T2,TR):-
    trace(TR),
    email(E1),
    email(E2),
    fi(A),
    emp(P),
    extAddr(S),
    time(T2),
    time(T1),
    T2 > T1,
    happens(emailSentExt2ExtD(E2,A,P1,S),T2,TR),
    happens(recEmailReqExt2E(E1,A,S,P),T1,TR).


% File F stored in computer C is copied by an employee who is logged to C

happens(copy(E,F,C),T,TR):-
    time(T), fi(F), comp(C), emp(E),
    trace(TR),
    happens(sys_copy(E,F,C),T,TR),
    holdsAt(isStoredIn(F,C),T,TR),
    holdsAt(logged(E,C),T,TR).

% An external storage device is mounted on a computer while an employee is logged to the computer

happens(mount(S,C),T, TR):-
    time(T), comp(C), st(S), emp(E),
    trace(TR),
    happens(sys_mount(S,C),T,TR),
    not holdsAt(mounted(S,C),T,TR),
    holdsAt(logged(E,C),T,TR).

% An external storage device who is currently mounted on a computer is unmounted

happens(unmount(S,C),T,TR):-
    time(T), comp(C), st(S),
    trace(TR),
    happens(sys_unmount(S,C),T,TR),
    holdsAt(mounted(S,C),T,TR).

% An application A is installed on computer C while an employee who is entitled to access 
% the computer is logged 

happens(install(A,C),T,TR):-
    app(A),
    comp(C),
    time(T),
    emp(P),
    trace(TR),
    happens(sys_install(A,C),T,TR),
    holdsAt(logged(P,C),T,TR),
    not holdsAt(installed(A,C),T,TR),
    holdsAt(hasPermission(P,C),T,TR).

% An application A that is currently installed on computer C is uninstalled while an employee who is entitled to access 
% the computer is logged 
happens(uninstall(A,C),T,TR):-
    trace(TR),
    app(A),
    comp(C),
    time(T),
    emp(P),
    happens(sys_uninstall(A,C),T,TR),
    holdsAt(logged(P,C),T,TR),
    holdsAt(installed(A,C),T,TR),
    holdsAt(hasPermission(P,C),T,TR).

% A web connection to url U is performed from a browser installed on a computer
% while an employee entitled to access the computer is logged

happens(webConnection(B,U,C),T,TR):-
    trace(TR),
    browser(B),
    url(U),
    comp(C),
    time(T),
    happens(web_connection(B,U,C),T,TR),
    holdsAt(isInstalled(B,C),T,TR),
    holdsAt(logged(E,C),T,TR),
    holdsAt(hasPermission(E,C),T,TR).




% +++ Context Relation Definitions +++

% If an employee performs the login to a computer, then she is logged to the computer

initiates(login(E,C),logged(E,C), T):-
    emp(E), comp(C), time(T).

% If an employee performs the logout from a computer, then she is no longer logged to the computer

terminates(logout(E,C),logged(E,C), T):-
    emp(E), comp(C), time(T).

% If a storage device is being mounted on a computer, then it is considered as mounted on the computer

initiates(mount(S,C), mounted(S,C), T):-
    st(S), comp(C), time(T).

% If a storage device is being unmounted from a computer, then it is no longer considered mounted on the computer

terminates(unmount(S,C), mounted(S,C), T):-
    st(S), comp(C), time(T).

% If an application is being installed on a computer, then it is considered as installed on the computer

initiates(install(A,C), installed(A,C), T):-
    app(A), comp(C), time(T).

% If an application is being uninstalled on a computer, then it is no longer installed on the computer

terminates(uninstall(A,C), installed(A,C), T):-
    app(A), comp(C), time(T).


%;******** Initial State *********

%Alison and Jean have permission to login to M2 and M1, respectively.

initially(hasPermission(jean,m1)).
initially(hasPermission(alison,m2)).

initially(hasEmail(jean,ajean)).
initially(hasEmail(alison,aalison)).

initially(hasPwd(jean, pwdJean)).
initially(hasPwd(alison, pwdAlison)).

initially(isStoredIn(m57plan,m1)).

initially(isMalware(app1)).

initially(isInstalled(b1,m1)).
initially(isInstalled(b2,m2)).

initially(isBlacklisted(url1)).


% ******************************************************************************